plugins {
    id "net.saliman.cobertura" version "2.3.0"
    id "com.github.kt3k.coveralls" version "2.6.3"
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group 'br.ufscar.sead.loa.propeller'
version '0.1.1'

apply plugin: 'groovy'

repositories {
    mavenCentral()
}

dependencies {
    compile ('org.codehaus.groovy:groovy-all:2.3.11',
             'org.mongodb:mongodb-driver:3.2.1',
             'org.mongodb.morphia:morphia:1.1.1')

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.3"
    testCompile 'org.slf4j:slf4j-simple:1.6.1'
}

sourceSets {
    test {
        groovy {
            srcDir 'test/unit'
        }
    }
}

cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report
cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':cobertura') || graph.hasTask(':test')) {
        compileGroovy.groovyOptions.optimizationOptions.all = false
    }
}

shadowJar {
    dependencies {
        exclude(dependency('org.codehaus.groovy:groovy-all:2.3.11'))
    }
}